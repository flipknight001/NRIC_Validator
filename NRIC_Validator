'SG NRIC Validator
'Converted from Code by Eddie Moore
'https://gist.github.com/eddiemoore/7131781
'Chee Keong CHAN
'03.05.2020
'Flipknight001
'Fixed issues with code bugging out with non-singapore NRIC.
'31/03/2022
'Clean up of code with chatGPI assistance
'05/02/2023
'Add support for M Series FIN
'17/7/2023

Public Function validateNRIC(ByVal NRIC As String) As Boolean
    
    'Check Length
    Const NRIC_LENGTH As Long = 9
    
    NRIC = UCase(NRIC)

    If Len(NRIC) <> NRIC_LENGTH Then
        validateNRIC = False
        Exit Function
    End If
    
    'Check if more than 2 Letters
    Dim FirstLetter As String
    Dim LastLetter As String
    Dim NumericPart As Variant
    
    FirstLetter = Left(NRIC, 1)
    LastLetter = Right(NRIC, 1)
    NumericPart = Mid(NRIC, 2, 7)

    If Not IsNumeric(NumericPart) Then
        validateNRIC = False
        Exit Function
    End If
    
    'Weightage for NRIC number
    Dim WEIGHTS()
    ReDim WEIGHTS(1 To 7)
    WEIGHTS(1) = 2
    WEIGHTS(2) = 7
    WEIGHTS(3) = 6
    WEIGHTS(4) = 5
    WEIGHTS(5) = 4
    WEIGHTS(6) = 3
    WEIGHTS(7) = 2

    Const ST_ALPHABET As String = "JZIHGFEDCBA"
    Const FG_ALPHABET As String = "XWUTRQPNMLK"
    Const M_ALPHABET As String = "KLJNPQRTUWX"
    
    'Set offset for G and T
    Dim Sum As Long
    If FirstLetter = "G" Or FirstLetter = "T" Then
        Sum = 4
    ElseIf FirstLetter = "M" Then
        Sum = 3
    Else
        Sum = 0
    End If

    For i = 1 To Len(NumericPart)
        Sum = Sum + (Mid(NumericPart, i, 1) * WEIGHTS(i))
    Next
    
    'Remainder used for sumcheck against LastLetter
    Dim Remainder As Long
    
    Remainder = Sum Mod 11
    Dim Alphabets As String

    If FirstLetter = "S" Or FirstLetter = "T" Then
        Alphabets = ST_ALPHABET
    ElseIf FirstLetter = "M" Then
        Alphabets = M_ALPHABET
    Else
        Alphabets = FG_ALPHABET
    End If

    validateNRIC = LastLetter = Mid(Alphabets, Remainder + 1, 1)
End Function
